plugins {
    id 'java'
    id 'org.springframework.boot' version '2.7.11'
    id 'io.spring.dependency-management' version '1.1.0'
    id 'io.freefair.lombok' version '6.0.0-m2'
}

def javaProjects = [
        project(":post:post-adapter"),
        project(":post:post-application"),
        project(":post:post-domain")
]
def springBootProjects = [
        project(":post:post-adapter"),
        project(":post:post-application")
]

configure(javaProjects) {
    apply plugin: "java"
    apply plugin: "io.freefair.lombok"

    tasks.withType(JavaCompile) {
        sourceCompatibility = "11"
        targetCompatibility = "11"
        options.encoding = "UTF-8"
    }

    project.sourceSets.test.resources.srcDirs = [ "${projectDir}/src/test/resources", "${rootDir}/schema" ]

    dependencies {
        implementation 'com.fasterxml.uuid:java-uuid-generator:4.1.0'
        implementation 'org.hibernate:hibernate-validator:6.2.0.Final'
        implementation 'javax.validation:validation-api:2.0.1.Final'
        implementation 'org.glassfish:javax.el:3.0.0'

        if (!springBootProjects.contains(project)) {
            testImplementation(platform('org.junit:junit-bom:5.7.1'))
            testImplementation 'org.junit.jupiter:junit-jupiter-api:5.9.2'
            testImplementation 'org.assertj:assertj-core:3.20.2'
            testImplementation 'org.mockito:mockito-junit-jupiter:5.3.1'
        }
    }
}

configure(springBootProjects) {
    apply plugin: "org.springframework.boot"
    apply plugin: "io.spring.dependency-management"

    dependencies {
        implementation 'org.springframework.boot:spring-boot-starter-data-jdbc'
        implementation 'org.springframework.boot:spring-boot-starter-web'
        implementation 'org.hibernate:hibernate-validator:6.2.0.Final'

        testImplementation "org.springframework.boot:spring-boot-starter-test"
    }
}

bootJar {
    enabled(false)
}
jar {
    enabled(true)
}